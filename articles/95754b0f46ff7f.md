---
title: "Rustãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆä¸Šã§ã€Nixã‚’ä½¿ç”¨ã—ã¦ãƒ“ãƒ«ãƒ‰ã€ãƒã‚§ãƒƒã‚¯ã€cargo-llvm-covã§ã®ãƒ†ã‚¹ãƒˆã‚«ãƒãƒ¬ãƒƒã‚¸ã‚’è¡Œãªã†æ–¹æ³•"
emoji: "ğŸ¤–"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["rust", "nix", "coverage", "githubactions"]
published: true
---

# TL;DR
[crane](https://github.com/ipetkov/crane/)ã¨ã„ã†Rustãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã‚’ãƒ“ãƒ«ãƒ‰ã™ã‚‹ã‚·ã‚¹ãƒ†ãƒ ã«ã‚ã‚‹ã€[cargoLlvmCové–¢æ•°](https://github.com/ipetkov/crane/blob/529c1a0b1f29f0d78fa3086b8f6a134c71ef3aaf/lib/cargoLlvmCov.nix)ã£ã¦ã‚¹ã‚´ã„ã‚ˆãªãã¨ã„ã†è©±ã€‚

# ã©ã‚“ãªflake.nixã‚’æ›¸ã„ãŸã‹
[flake.nix in kosu project](https://github.com/haruki7049/kosu/blob/4e03f5cb36c1010fa5a541aeb18b151be7ea0362/flake.nix)
```nix
{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    treefmt-nix.url = "github:numtide/treefmt-nix";
    rust-overlay.url = "github:oxalica/rust-overlay";
    crane.url = "github:ipetkov/crane";
  };

  outputs = { self, nixpkgs, treefmt-nix, rust-overlay, flake-utils, crane }:
    flake-utils.lib.eachSystem [ "x86_64-linux" "aarch64-linux" ] (system:
      let
        overlays = [ (import rust-overlay) ];
        pkgs = import nixpkgs {
          inherit overlays system;
        };
        lib = pkgs.lib;
        treefmtEval = treefmt-nix.lib.evalModule pkgs ./treefmt.nix;
        rust = pkgs.rust-bin.fromRustupToolchainFile ./rust-toolchain.toml;
        craneLib = (crane.mkLib pkgs).overrideToolchain rust;
        src = ./.;
        cargoArtifacts = craneLib.buildDepsOnly {
          inherit src;
          buildInputs = bevyengine-dependencies;
          nativeBuildInputs = [ pkgs.pkg-config ];
        };
        kosu = craneLib.buildPackage {
          inherit src cargoArtifacts;
          buildInputs = bevyengine-dependencies;
          nativeBuildInputs = [ pkgs.pkg-config ];
          strictDeps = true;

          doCheck = true;
        };
        cargo-clippy = craneLib.cargoClippy {
          inherit cargoArtifacts src;
          buildInputs = bevyengine-dependencies;
          nativeBuildInputs = [ pkgs.pkg-config ];
          cargoClippyExtraArgs = "--verbose -- --deny warnings";
        };
        cargo-doc = craneLib.cargoDoc {
          inherit cargoArtifacts src;
          buildInputs = bevyengine-dependencies;
          nativeBuildInputs = [ pkgs.pkg-config ];
        };
        bevyengine-dependencies = with pkgs; [
          udev
          alsa-lib
          vulkan-loader

          # To use the x11 feature
          xorg.libX11
          xorg.libXcursor
          xorg.libXi
          xorg.libXrandr

          # To use the wayland feature
          libxkbcommon
          wayland
        ];
        llvm-cov-text = craneLib.cargoLlvmCov {
          inherit cargoArtifacts src;
          buildInputs = bevyengine-dependencies;
          nativeBuildInputs = [ pkgs.pkg-config ];
          cargoExtraArgs = "--locked";
          cargoLlvmCovCommand = "test";
          cargoLlvmCovExtraArgs = "--text --output-dir $out";
        };
        llvm-cov = craneLib.cargoLlvmCov {
          inherit cargoArtifacts src;
          buildInputs = bevyengine-dependencies;
          nativeBuildInputs = [ pkgs.pkg-config ];
          cargoExtraArgs = "--locked";
          cargoLlvmCovCommand = "test";
          cargoLlvmCovExtraArgs = "--html --output-dir $out";
        };
      in
      {
        formatter = treefmtEval.config.build.wrapper;

        packages.default = kosu;
        packages.doc = cargo-doc;
        packages.llvm-cov = llvm-cov;
        packages.llvm-cov-text = llvm-cov-text;

        apps.default = flake-utils.lib.mkApp {
          drv = self.packages.${system}.default;
        };

        checks = {
          inherit kosu cargo-clippy cargo-doc llvm-cov llvm-cov-text;
          formatting = treefmtEval.config.build.check self;
        };

        devShells.default = pkgs.mkShell rec {
          buildInputs = bevyengine-dependencies ++ [
            rust
          ];

          nativeBuildInputs = [
            pkgs.pkg-config
          ];

          LD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;

          shellHook = ''
            export PS1="\n[nix-shell:\w]$ "
          '';
        };
      });
}
```

## ä»¥ä¸Šã®flake.nixã®è§£èª¬
`flake.nix`ã¨ã„ã†ãƒ¢ãƒã¯ã€ãƒ‘ãƒƒã‚±ãƒ¼ã‚¸ã®ãƒ“ãƒ«ãƒ‰å¼(æ­£ç¢ºã«ã¯Derivationã¨è¨€ã†)ã‚’å®£è¨€å‡ºæ¥ãŸã‚Šã€ãƒã‚§ãƒƒã‚¯ã‚’ä½œæˆã—ã¦èµ°ã‚‰ã›ãŸã‚Šã€é–‹ç™ºç’°å¢ƒã‚’æ•´ãˆãŸã‚Šã§ãã‚‹ä»£ç‰©ã§ã‚ã‚‹ã€‚ä»–ã«ã‚‚ãƒ•ã‚©ãƒ¼ãƒãƒƒã‚¿ã‚’æŒ‡å®šã—ã¦ã€`nix fmt`ã¨æ‰“ã¤ã ã‘ã§ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆå…¨ä½“ã®ãƒ•ã‚©ãƒ¼ãƒãƒƒãƒˆã‚’èµ°ã‚‰ã›ãŸã‚Šã€[nix-overlay](https://wiki.nixos.org/wiki/Overlays)ã‚’ä½œæˆã™ã‚‹äº‹ã ã£ã¦å‡ºæ¥ã‚‹ã€‚

ä»Šå›ä½œæˆã—ãŸéƒ¨åˆ†ã¯ã€`let - in`æ–‡ã®éƒ¨åˆ†ã«æ›¸ã‹ã‚Œã¦ã„ã‚‹ã€‚`llvm-cov`ã¨`llvm-cov-text`ã¨ã„ã†åå‰ã§å®£è¨€ã•ã‚Œã¦ã„ã‚‹ã€‚ã“ã“ã§ä½¿ç”¨ã•ã‚Œã¦ã„ã‚‹é–¢æ•°ã€`cargoLlvmCov`ã¯`craneLib`ã‚’å®šç¾©ã—ãªã„ã¨ä½¿ç”¨å‡ºæ¥ãªã„ç‚¹ã‚’ç•™æ„ã—ã¦æ¬²ã—ã„ã€‚

`cargoLlvmCov`é–¢æ•°ã¯ã€[ã‚³ã‚³](https://github.com/ipetkov/crane/blob/529c1a0b1f29f0d78fa3086b8f6a134c71ef3aaf/lib/cargoLlvmCov.nix)ã«å®šç¾©ã•ã‚Œã¦ã„ã‚‹ã€‚

ä»¥ä¸Šã®å¼ã‚’æ›¸ãã¨ã€`nix build .#llvm-cov`ã¨CLIä¸Šã§æ‰“ã¤ã¨HTMLå½¢å¼ã®ãƒ†ã‚¹ãƒˆã‚«ãƒãƒ¬ãƒƒã‚¸ã®çµæœãŒãƒ“ãƒ«ãƒ‰ã•ã‚Œã‚‹ã€‚`nix build .#llvm-cov-text`ã¨æ‰“ã¤ã¨ã€`packages.llvm-cov-text`ã«`llvm-cov-text`ã®æ–¹ã®å®šç¾©ãŒç´ä»˜ã‘ã‚‰ã‚Œã¦ã„ã‚‹ãŸã‚ã€ãƒ†ã‚­ã‚¹ãƒˆå½¢å¼ã®ãƒ†ã‚¹ãƒˆã‚«ãƒãƒ¬ãƒƒã‚¸ã®çµæœãŒãƒ“ãƒ«ãƒ‰ã•ã‚Œã‚‹äº‹ã¨ãªã‚‹ã€‚

ã“ã®æ™‚ç‚¹ã§ã®æ³¨æ„ç‚¹ã¯ã€`cargoLlvmCov`ã«`buildInputs`ã¨`nativeBuildInputs`ã‚’å¼•æ•°ã«å«ã‚ã‚‹äº‹ãŒå¯èƒ½ã§ã‚ã‚‹ã¨ã„ã†ç‚¹ã ã€‚ã“ã‚Œã‚‰ã‚’å«ã‚ãªã„ã¨ã€ã‚«ãƒãƒ¬ãƒƒã‚¸ã®ç”ŸæˆãŒå¤±æ•—ã—ã¦ã—ã¾ã†ã‚±ãƒ¼ã‚¹ãŒã‚ã‚‹(ä¾‹ãˆã‚‹ã¨ã€`pkgs-config`ã‚’`nativeBuildInputs`ã«ä½¿ç”¨ã—ã¦ã„ã‚‹ã‚±ãƒ¼ã‚¹ãªã©)ã€‚

# GitHub Actionsã§ã®CI
Nixã‚’æ›¸ãã¨ã„ã†äº‹ã¯ã€æ‰‹å…ƒã§ã®ãƒ“ãƒ«ãƒ‰ã®ã¿ã‚’æ›¸ã„ã¦ã„ã‚‹è¨³ã§ã¯ãªã„ã€‚ã›ã£ã‹ããƒ“ãƒ«ãƒ‰ã®å†ç¾æ€§ãŒæ‹…ä¿ã•ã‚Œã¦ã„ã‚‹ã¨ã„ã†ã®ã«ã€CIã§ä½¿ç”¨ã—ãªã„ã¨ã„ã†äº‹ã¯ã€å®ã®æŒã¡è…ã‚Œã§ã‚ã‚‹(è¨€ã„ã™ãã‹ã‚‚â€¦ï¼Ÿ)ã€‚ãã“ã§ã€ä»Šå›ã¯GitHub Actionsã§`cargo-llvm-cov`ã®çµæœã‚’ã€Nixã§ãƒ“ãƒ«ãƒ‰ã—ã¦ã¿ã‚ˆã†ã§ã¯ãªã„ã‹ã€‚

# ä»Šå›æ›¸ã„ãŸWorkflow
```yaml
name: nix-checker
on:
  push:
permissions: {}
jobs:
  run-nix-check:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@b4ffde65f46336ab88eb53be808477a3936bae11 # v4.1.1
      - name: Install Nix
        uses: cachix/install-nix-action@v26
        with:
          nix_path: nixpkgs=channel:nixos-unstable
      - name: Run nix flake check
        run: nix flake check --all-systems

  run-nix-build:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@b4ffde65f46336ab88eb53be808477a3936bae11 # v4.1.1
      - name: Install Nix
        uses: cachix/install-nix-action@v26
        with:
          nix_path: nixpkgs=channel:nixos-unstable
      - name: Run nix-build with flakes
        run: nix build .#default

  run-nix-build-doc:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@b4ffde65f46336ab88eb53be808477a3936bae11 # v4.1.1
      - name: Install Nix
        uses: cachix/install-nix-action@v26
        with:
          nix_path: nixpkgs=channel:nixos-unstable
      - name: Run nix-build with flakes
        run: nix build .#doc

  build-cargo-llvm-cov-text:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@b4ffde65f46336ab88eb53be808477a3936bae11 # v4.1.1
      - name: Install Nix
        uses: cachix/install-nix-action@v26
        with:
          nix_path: nixpkgs=channel:nixos-unstable
      - name: Run nix-build with flakes
        run: nix build .#llvm-cov-text
      - name: Upload coverage
        uses: actions/upload-artifact@v4
        with:
          name: coverage-text
          path: result

  build-cargo-llvm-cov:
    runs-on: ubuntu-latest
    steps:
      - name: Checkout
        uses: actions/checkout@b4ffde65f46336ab88eb53be808477a3936bae11 # v4.1.1
      - name: Install Nix
        uses: cachix/install-nix-action@v26
        with:
          nix_path: nixpkgs=channel:nixos-unstable
      - name: Run nix-build with flakes
        run: nix build .#llvm-cov
      - name: Upload coverage
        uses: actions/upload-artifact@v4
        with:
          name: coverage-html
          path: result
```

## Workflowã®è§£èª¬
ã¾ãšã¯ã€`run-nix-*`ãŸã¡ã‚’è§£èª¬ã™ã‚‹ã€‚ã²ã¨ã¾ãšã€ã“ã‚Œã‚‰ã®åå‰ã‚’åˆ—æŒ™ã™ã‚‹ã€‚
- run-nix-check
- run-nix-build
- run-nix-build-doc

ã“ã‚Œã‚‰ã®Workflowã§ã‚„ã£ã¦ã„ã‚‹äº‹ã¯å˜ç´”ã§ã€å˜ã«Ubuntuä¸Šã«Nixã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã—ã¦ã€`nix build .#default`ã‚„`nix build .#doc`ã‚’å®Ÿè¡Œã—ã¦ã„ã‚‹ã ã‘ãªã®ã§ã‚ã‚‹ã€‚ã¡ãªã¿ã«ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã®ãƒ›ã‚¹ãƒ†ã‚£ãƒ³ã‚°ã¯ã“ã“ã§ã¯è¡Œãªã£ã¦ã„ãªã„ã€‚ã“ã‚Œã‚’ã™ã‚‹ã¨ä½•ãŒå¬‰ã—ã„ã‹ã¨ã„ã†ã¨ã€ä»¥ä¸‹ã®ç‚¹ãŒæŒ™ã’ã‚‰ã‚Œã‚‹ã€‚
1. æ‰‹å…ƒã§ã‚‚åŒæ§˜ã«ãƒ“ãƒ«ãƒ‰ãŒå‡ºæ¥ã‚‹ç‚¹
2. æ‰‹å…ƒã§ãƒ“ãƒ«ãƒ‰ã—ã¦ã‚‚åŒæ§˜ã®çµæœã«ãªã‚‹ç‚¹
3. WorkflowãŒå˜ç´”ã«ãªã‚Šã€èª­ã¿ã‚„ã™ããªã‚‹ç‚¹

æ¬¡ã«ã€`build-cargo-llvm-cov*`ãŸã¡ã‚’è§£èª¬ã™ã‚‹ã€‚ã¨ã„ã£ã¦ã‚‚ã€å…ˆç¨‹è©±ã—ãŸç‚¹ã¨ã‚ã¾ã‚Šå¤‰ã‚ã‚‰ãªã„ã€‚å˜ã«Ubuntuä¸Šã«Nixã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã—ã¦ã€`nix build .#llvm-cov`ã‚„`nix build .#llvm-cov-text`ã‚’å®Ÿè¡Œã—ã¦ã„ã‚‹ã ã‘ã§ã‚ã‚‹ã€‚ã“ã¡ã‚‰ã§ã¯ã€ãƒ“ãƒ«ãƒ‰ã®æˆæœç‰©ã‚’ã‚¢ãƒ¼ãƒ†ã‚£ãƒ•ã‚¡ã‚¯ãƒˆã¨ã—ã¦ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ã—ã¦ã„ã‚‹ã€‚

# æ³¨æ„ç‚¹
ç§ã®æ„Ÿè¦šã§ã¯ã‚ã‚‹ãŸã‚ã€ã¯ã£ãã‚Šã¨ã—ãŸäº‹ã¯è¨€ãˆãªã„ãŒã€ã“ã®CIã®å®Ÿè¡Œæ–¹æ³•ã«ã¯ã€ã‚ã‚‹å¼±ç‚¹ãŒã‚ã‚‹ã€‚ãã‚Œã¯ã€ãƒ“ãƒ«ãƒ‰æ™‚é–“ãŒé•·ããªã‚‹ã¨ã„ã†ç‚¹ã (ä½†ã—ã€Nixã‚ã‚Šã¨ç„¡ã—ã¨ã§æ­£ç¢ºã«æ™‚é–“ã‚’ã¯ã‹ã£ãŸäº‹ã¯ãªã„â€¦)ã€‚ãã®ãŸã‚ã«ã€ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆãƒªãƒã‚¸ãƒˆãƒªã ã£ãŸã‚Š`GitLab CI`ã‚’ä½¿ç”¨ã™ã‚‹ã¨ã€æ™‚é–“åˆ¶é™ã«æ‚©ã¾ã•ã‚Œã‚‹ã€‚ã‚‚ã—ã‚‚ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆãƒªãƒã‚¸ãƒˆãƒªã‚„`GitLab CI`ã‚’ä½¿ç”¨ã—ã¦ã„ã‚‹éš›ã«ã¯ã€åˆ¥ã®ãƒ“ãƒ«ãƒ‰æ–¹æ³•ã‚„ãƒ“ãƒ«ãƒ‰æ™‚é–“è»½æ¸›ã‚’è€ƒãˆãŸæ–¹ãŒè‰¯ã„ã‹ã‚‚ã—ã‚Œãªã„ã€‚
